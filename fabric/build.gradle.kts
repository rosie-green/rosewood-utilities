plugins {
    id("generic-java")
    id("net.fabricmc.fabric-loom")
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = uri("https://api.modrinth.com/maven")
            }
        }

        filter {
            includeGroup("maven.modrinth")
        }
    }
}

val minecraftVersion = providers.gradleProperty("minecraft_version").get()
val fabricLoaderVersion = providers.gradleProperty("fabric_loader_version").get()
val fabricApiVersion = providers.gradleProperty("fabric_api_version").get()
val projectId = providers.gradleProperty("project_id").get()

dependencies {
    minecraft("com.mojang:minecraft:$minecraftVersion")
    implementation("net.fabricmc:fabric-loader:$fabricLoaderVersion")
    implementation("net.fabricmc.fabric-api:fabric-api:$fabricApiVersion")

    runtimeOnly(providers.gradleProperty("runtime.modmenu").map { "maven.modrinth:modmenu:$it" })
}

loom {
    splitEnvironmentSourceSets()

    mods {
        create(projectId) {
            sourceSet(sourceSets.main.get())
            sourceSet(sourceSets.named("client").get())
        }
    }

    runs {
        configureEach {
            generateRunConfig = true
            runDirectory = layout.projectDirectory.dir("runs/$name")

            displayName = "Fabric ${name.replaceFirstChar(Char::uppercase)}"
        }

        named("client") { client() }
        named("server") { server() }
    }
}
